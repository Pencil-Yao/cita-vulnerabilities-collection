# 标题

SWC-124 写入任意存储位置（Write to Arbitrary Storage Location）

## 漏洞等级

高

## 描述

智能合约的数据（例如，存储合约的所有者）被永久存储在EVM的某个存储位置。合约负责确保只有授权的用户或合约帐户才能写入敏感的存储位置。如果攻击者能够写入合约的任意存储位置，则可以很容易地规避授权检查。这可能使攻击者破坏存储；例如，通过覆盖存储合约所有者地址的字段。

## CWE漏洞分类

[CWE-123: Write-what-where 条件](https://cwe.mitre.org/data/definitions/123.html)

## 修改建议

作为一般性建议，鉴于所有数据结构共享相同的存储（地址）空间，应确保对一个数据结构的写入不会无意间覆盖另一数据结构的条目。

## 参考链接

- [2017 solidity代码大赛](https://github.com/Arachnid/uscc/tree/master/submissions-2017/doughoyte)

## 案例

### arbitrary_location_write_simple.sol

```solidity
pragma solidity ^0.4.25;

contract Wallet {
    uint[] private bonusCodes;
    address private owner;

    constructor() public {
        bonusCodes = new uint[](0);
        owner = msg.sender;
    }

    function () public payable {
    }

    function PushBonusCode(uint c) public {
        bonusCodes.push(c);
    }

    function PopBonusCode() public {
        require(0 <= bonusCodes.length);
        bonusCodes.length--;
    }

    function UpdateBonusCodeAt(uint idx, uint c) public {
        require(idx < bonusCodes.length);
        bonusCodes[idx] = c;
    }

    function Destroy() public {
        require(msg.sender == owner);
        selfdestruct(msg.sender);
    }
}

```

### arbitrary_location_write_simple_fixed.sol

```solidity
pragma solidity ^0.4.25;

contract Wallet {
    uint[] private bonusCodes;
    address private owner;

    constructor() public {
        bonusCodes = new uint[](0);
        owner = msg.sender;
    }

    function () public payable {
    }

    function PushBonusCode(uint c) public {
        bonusCodes.push(c);
    }

    function PopBonusCode() public {
        require(0 < bonusCodes.length);
        bonusCodes.length--;
    }

    function UpdateBonusCodeAt(uint idx, uint c) public {
        require(idx < bonusCodes.length); //Since you now have to push very codes this is no longer an arbitray write.
        bonusCodes[idx] = c;
    }

    function Destroy() public {
        require(msg.sender == owner);
        selfdestruct(msg.sender);
    }
}

```

### mapping_write.sol

```solidity
pragma solidity ^0.4.24;

//This code is derived from the Capture the Ether https://capturetheether.com/challenges/math/mapping/

contract Map {
    address public owner;
    uint256[] map;

    function set(uint256 key, uint256 value) public {
        if (map.length <= key) {
            map.length = key + 1;
        }

        map[key] = value;
    }

    function get(uint256 key) public view returns (uint256) {
        return map[key];
    }
    function withdraw() public{
      require(msg.sender == owner);
      msg.sender.transfer(address(this).balance);
    }
}

```
