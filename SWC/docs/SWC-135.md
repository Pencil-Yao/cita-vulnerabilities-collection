# 标题

SWC-135 无效代码（Code With No Effects）

## 漏洞等级

高

## 描述

在Solidity中，可以编写未产生预期效果的代码。当前，solidity编译器将不会为无效代码返回警告。这可能导致引入无法正确执行预期动作的“死”代码。

例如，`msg.sender.call.value(address(this).balance)("");`很容易漏掉最后的括号，这可能导致函数继续执行而无需将资金转入msg.sender。虽然，应该通过检查call的返回值来避免这种情况（详见SWC-104）。

## CWE漏洞分类

[CWE-1164：不相关的代码](https://cwe.mitre.org/data/definitions/1164.html)

## 修改建议

务必确保合约按预期工作，这一点很重要。编写单元测试以验证代码的正确行为。

## 参考链接

- [Issue on Solidity's Github - 当语句永远不会有副作用时引发错误](https://github.com/ethereum/solidity/issues/2707)
- [Issue on Solidity's Github - msg.sender.call.value(address(this).balance); 应该发出警告](https://github.com/ethereum/solidity/issues/7096)

## 案例

### deposit_box.sol

```solidity
pragma solidity ^0.5.0;

contract DepositBox {
    mapping(address => uint) balance;

    // Accept deposit
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'incorrect amount');
        // Should update user balance
        balance[msg.sender] == amount;
    }
}
```

### deposit_box_fixed.sol

```solidity
pragma solidity ^0.5.0;

contract DepositBox {
    mapping(address => uint) balance;

    // Accept deposit
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'incorrect amount');
        // Should update user balance
        balance[msg.sender] = amount;
    }
}
```

### wallet.sol

```solidity
/*
 * @author: Kaden Zipfel
 */

pragma solidity ^0.5.0;

contract Wallet {
    mapping(address => uint) balance;

    // Deposit funds in contract
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'msg.value must be equal to amount');
        balance[msg.sender] = amount;
    }

    // Withdraw funds from contract
    function withdraw(uint amount) public {
        require(amount <= balance[msg.sender], 'amount must be less than balance');

        uint previousBalance = balance[msg.sender];
        balance[msg.sender] = previousBalance - amount;

        // Attempt to send amount from the contract to msg.sender
        msg.sender.call.value(amount);
    }
}
```

### wallet_fixed.sol

```solidity
/*
 * @author: Kaden Zipfel
 */

pragma solidity ^0.5.0;

contract Wallet {
    mapping(address => uint) balance;

    // Deposit funds in contract
    function deposit(uint amount) public payable {
        require(msg.value == amount, 'msg.value must be equal to amount');
        balance[msg.sender] = amount;
    }

    // Withdraw funds from contract
    function withdraw(uint amount) public {
        require(amount <= balance[msg.sender], 'amount must be less than balance');

        uint previousBalance = balance[msg.sender];
        balance[msg.sender] = previousBalance - amount;

        // Attempt to send amount from the contract to msg.sender
        (bool success, ) = msg.sender.call.value(amount)("");
        require(success, 'transfer failed');
    }
}
```
